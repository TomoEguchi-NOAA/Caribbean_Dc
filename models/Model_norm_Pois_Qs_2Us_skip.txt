# simple state space AR1 model for turtle nesting 
# original code from Lab 7, Introduction to Bayesian Time-Series
# analysis using Jags. Univ Washington: 
# https://www.scribd.com/document/373707632/Lab-7-Fitting-models-with-JAGS-pdf

# Also can be found here:
# https://nwfsc-timeseries.github.io/atsa-labs/sec-jags-uss.html
#
# For this model, the raw counts were used as data (y). The state model
# is normal and the observation model is Poisson. I added skip nesting
# in this model by having a proportion of Nt available for sampling.
#
# I didn't see any reason to make the priors flatter because that would make convergence difficult
# and did not gain anything in return. 
#
# In this approach, I use two Us (population grwoth rate); one for up to 2011 and another for 
# since 2011. It appears that there are decline at some beaches.

# This model was converted from one for the Indonesia leatherback nesting project.
# 2020-04-10 (original)
# 2020-04-14 (2Us)
# 2020-04-16 (skip nesting)
#
# Provide the following variables in data 
# n.beaches = the number of beaches (a scalar)
# n.years = the number of years of observations per beach (a vector)
# years = observation years in 1 or 2, where 1 is before the change year and 2 is for since the change year (a matrix of n.beaches x max(n.years)). This needs to match with y below.
# y = observed number of nests/females per year (a matrix of n.beaches x max(n.years))
# high.low = high/low years in 1 or 2. It is a matrix of n.beaches x max(n.years), where 1 and 2 alternate from the first year to the end. This parameter is trying to incorporate high/low cyclical nature of observed nesting.


model{
    # Initial states
    # all beaches started with very low numbers (similar among them)
    # I will have just one common N0 value.

    mean.N0 ~ dnorm(0, 0.1)T(0,)
    sigma.N0 ~ dgamma(2, 0.5)
    tau.N0 <- 1/(sigma.N0 * sigma.N0)
    
    # one U for before 2011 and another since 2011
    mean.U1 ~ dnorm(0, 0.1)T(0,)
    sigma.U1 ~ dgamma(2, 0.5)
    tau.U1 <- 1/(sigma.U1 * sigma.U1) 

    # for since 2011
    mean.U2 ~ dnorm(0, 0.1)T(0,)
    sigma.U2 ~ dgamma(2, 0.5)
    tau.U2 <- 1/(sigma.U2 * sigma.U2) 

	# Initial state
    for(j in 1:n.ID) {
        U[ID.1[j],1] ~ dnorm(mean.U1, tau.U1)
        U[ID.1[j],2] ~ dnorm(mean.U2, tau.U2)

		# high and low years
        p[ID.1[j],1] ~ dbeta(1, 1)
        p[ID.1[j],2] ~ dbeta(1, 1)
		
        # state     
        N0[ID.1[j]] ~ dnorm(mean.N0, tau.N0)T(0,)    # prior on init state

        pred.N[ID.1[j],1] <- N0[ID.1[j]] * U[ID.1[j], years.1[ID.1[j]]]  
        N[ID.1[j], 1] <- pred.N[ID.1[j], 1]
        
        # observation
        y.1[j] ~ dpois(p[ID.1[j], high.low.1[ID.1[j]]] * N[ID.1[j], 1]) 
                
        loglik[j] <- logdensity.pois(y.1[j], p[ID.1[j], high.low[ID.1[j]]] * N[ID.1[j], 1])
				
        # Q is the variance of the process (N)
        sigma.Q[ID.1[j]] ~ dgamma(2, 0.5)
        tau.Q[ID.1[j]] <- 1/(sigma.Q[ID.1[j]] * sigma.Q[ID.1[j]])                       

    }


    ####  End of initial states ####  
      
	for (j in 1:n.2plus){
        # State
        pred.N[ID.2[j], year.2[j]] <- U[ID.2[j], years.2[ID.2[j]]] * N[ID.2[j], (year.2[j]-1)]
        N[ID.2[j], year.2[j]] ~ dnorm(pred.N[ID.2[j], year.2[j]], tau.Q[ID.2[j]])T(0,)

        # observation
        y.2[j] ~  dpois(p[ID.2[j], high.low.2plus[ID.2[j]]] * N[ID.2[j], year.2[j]])
                
        loglik[j+n.ID] <- logdensity.pois(y.2[j], p[ID.2[j], high.low[ID.2[j]]] * N[ID.2[j],year.2[j]])		
    }
      
    #mu ~ dnorm(0, 0.1)
    
}
